test "change modes":
  set {_x} to 0
  assert {_x} is 0 with "{_x} should be set to 0"
  add 1 to {_x}
  assert {_x} is 1 with "{_x} should be incremented to 1"
  remove 1 from {_x}
  assert {_x} is 0 with "{_x} should be decremented to 1"
  delete {_x}
  assert {_x} is not set with "{_x} should be deleted"

  add "a" to {_letters::*}
  assert {_letters::1} is "a" with "{_letters::1} should be set to 'a'"
  add "b" to {_letters::*}
  assert {_letters::2} is "b" with "{_letters::2} should be set to 'b'"
  remove "b" from {_letters::*}
  assert {_letters::2} is not set with "{_letters::2} should be removed"
  clear {_letters::*}
  assert amount of {_letters::*} is 0 with "{_letters::*} should be cleared"

  set {_isToggled} to false
  assert {_isToggled} is false with "{_isToggled} should be set to false"
  toggle {_isToggled}
  assert {_isToggled} is true with "{_isToggled} should be toggled to true"
  set {_toggle::*} to false, false and false
  assert {_toggle::1} is false with "{_toggle::1} should be set to false"
  assert {_toggle::2} is false with "{_toggle::2} should be set to false"
  assert {_toggle::3} is false with "{_toggle::3} should be set to false"
  toggle {_toggle::*}
  assert {_toggle::1} is true with "{_toggle::1} should be toggled to true"
  assert {_toggle::2} is true with "{_toggle::2} should be toggled to true"
  assert {_toggle::3} is true with "{_toggle::3} should be toggled to true"
  assert toggled {_isToggled} is false with "{_isToggled} should be toggled to false"

  set {_block} to block at location(10, 10, 10, world "world")
  set {_list::*} to true and {_block}
  toggle {_list::*}
  assert {_list::1} is false with "{_list::1} should be set to false"
  assert amount of {_list::*} is 2 with "{_list::*} should be set to false and {_block}"
